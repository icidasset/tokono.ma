set dotenv-load


default: build
  just serve & just watch


build:
  rm -rf dist
  mkdir -p dist
  cp -R ./static/ ./dist/
  pkgx npx tailwindcss -- --config=tailwind.config.js --input=./css/main.css --output=./dist/stylesheet.css --minify
  cd build && gren make Build.gren
  cd build && pkgx node app
  rm build/app
  cd dynamic && gren make Main.gren --output=../dist/site.js --optimize


format-gren:
  cd build && gren format ../ --yes


publish: build
  #!/usr/bin/env bash
  cid=$(pkgx npx w3 -- up ./dist/ | tail -n 1 | sed "s/‚ÅÇ https:\/\/w3s.link\/ipfs\///")
  # https://dns-lexicon.readthedocs.io/
  echo "Publishing $cid"
  lexicon cloudflare --auth-token $CLOUDFLARE_AUTH_TOKEN --content $cid update --name _dnslink _dnslink.tokono.ma TXT


@serve:
  echo "Serving static files on http://localhost:5001"
  dufs --port 5001 --render-index ./dist/


watch:
  watchexec --postpone --watch ./views --watch ./dynamic --watch ./css -- just build
