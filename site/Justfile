set dotenv-load


default: build
  just serve & just watch


build: build-folder build-css build-static-gren # build-dynamic-gren
build-optimised: build-folder build-css build-static-gren # build-dynamic-gren-optimised


build-folder:
  rm -rf dist
  mkdir -p dist
  cp -R ./static/* ./dist/


build-css:
  pkgx npx tailwindcss -- --config=tailwind.config.js --input=./css/main.css --output=./dist/stylesheet.css --minify


build-static-gren:
  cd build && gren make Build.gren
  cd build && pkgx node app
  rm build/app


build-dynamic-gren:
  cd dynamic && gren make Main.gren --output=../dist/site.js


build-dynamic-gren-optimised:
  cd dynamic && gren make Main.gren --output=../dist/site.js


format-gren:
  cd build && gren format ../ --yes


gather-notes:
  rm -rf ./notes/
  mkdir notes
  obsidian-export "/Users/steven/Library/Mobile Documents/iCloud~md~obsidian/Documents/Writings" --start-at "/Users/steven/Library/Mobile Documents/iCloud~md~obsidian/Documents/Writings/Notes on" --only-tags public "./notes/" --frontmatter never


publish: build-optimised
  npx netlify -- deploy --site tokonoma --dir ./dist --prod


@serve:
  echo "Serving static files on http://localhost:5001"
  dufs --port 5001 --render-spa ./dist/


watch:
  watchexec --postpone --watch ./views --watch ./dynamic --watch ./css -- just build
