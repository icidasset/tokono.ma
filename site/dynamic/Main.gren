module Main exposing ( main )

import Browser exposing ( UrlRequest(..) )
import Browser.Navigation
import Dict
import Essays
import Json.Decode
import Markdown
import Messages as ViewMsg
import Pages
import String.Extra as String
import Types exposing (..)
import Url exposing ( Url )
import View exposing ( view )



-- â›©ï¸


type alias Flags =
    { essays :
        Array
            { baseName : String
            , markdown : String
            }
    , pageRoute : Array String
    }


main : Program Flags Model Msg
main =
    Browser.application
        { init = init
        , view = view
        , update = update
        , subscriptions = subscriptions
        , onUrlRequest = UrlRequested
        , onUrlChange = UrlChanged
        }



-- ðŸŒ³


init :
    Flags
    -> Url
    -> Browser.Navigation.Key
    -> { model : Model
       , command : Cmd Msg
       }
init flags url key =
    let
        essays =
            flags.essays
                |> Array.filterMap
                        (\{ baseName, markdown } ->
                            markdown
                                |> Markdown.parse
                                        { frontmatter = Just (Markdown.jsonFrontmatter Essays.frontmatterDecoder)
                                        }
                                |> (\{ metadata, blocks } ->
                                        Maybe.map
                                            (\m ->
                                                { metadata = m
                                                , blocks = blocks
                                                }
                                            )
                                            metadata
                                    )
                                |> Maybe.map
                                        (\v ->
                                            { key = baseName
                                            , value = v
                                            }
                                        )
                        )
                |> Dict.fromArray

        essayTableOfContents =
            essays
                |> Dict.toArray
                |> Array.map
                        (\item ->
                            { page =
                                Pages.essay
                                    { baseName = item.key
                                    , title = item.value.metadata.title
                                    }
                            , archived = item.value.metadata.archived
                            }
                        )
                |> Array.sortBy (.page >> .title)

        rootUrl =
            url.path
                |> String.removePrefix "/"
                |> String.removeSuffix "/"
                |> String.split "/"
                |> Array.dropLast (Array.length flags.pageRoute)
                |> (\path ->
                        case path of
                            [] ->
                                ""

                            [ "" ] ->
                                ""

                            _ ->
                                "/" ++ String.join "/" path ++ "/"
                    )
                |> (\path -> { url | path = path })
    in
    { model =
        { essays = essays
        , essayTableOfContents = essayTableOfContents
        , navKey = key
        , rootUrl = rootUrl
        , url = url
        }
    , command = Cmd.none
    }



-- ðŸ“£


update :
    Msg
    -> Model
    -> { model : Model
       , command : Cmd Msg
       }
update msg model =
    case msg of
        ViewMsg (ViewMsg.NavigateToPage page) ->
            let
                url =
                    "/" ++ model.rootUrl.path ++ String.join "/" page.route ++ "/"
            in
            { model = model
            , command = Browser.Navigation.pushUrl model.navKey url
            }

        -- NAVIGATION
        UrlChanged url ->
            { model = { model | url = url }
            , command = Cmd.none
            }

        UrlRequested (External string) ->
            { model = model
            , command = Browser.Navigation.load string
            }

        UrlRequested (Internal url) ->
            { model = model
            , command = Browser.Navigation.pushUrl model.navKey (Url.toString url)
            }



-- ðŸ“°


subscriptions model =
    Sub.none
