module View exposing (..)

import Browser exposing ( Document )
import Dict
import Json.Decode
import Layouts.Markdown
import Markdown
import Pages
import String.Extra as String
import Transmutable.Html
import Transmutable.Html.VirtualDom
import Types exposing ( Model, Msg(..) )
import Url
import Views.Pages


view : Model -> Document Msg
view model =
    let
        pageRoute =
            case
                model.url.path
                    |> String.removePrefix "/"
                    |> String.removeSuffix "/"
                    |> String.split "/"
            of
                [ "" ] ->
                    []

                r ->
                    r

        viewOpts =
            { essayTableOfContents = model.essays.tableOfContents
            , notesTableOfContents = model.notes.tableOfContents
            }

        maybe =
            case pageRoute of
                [ "essays", baseName ] ->
                    model.essays.collection
                        |> Dict.get baseName
                        |> Maybe.map
                                (\{ blocks, title, publishedOn } ->
                                    let
                                        p =
                                            Pages.essay
                                                { baseName = baseName
                                                , title = title
                                                }
                                    in
                                    { page = p
                                    , view =
                                        blocks
                                            |> Array.filterMap (Json.Decode.decodeValue Markdown.jsonDecoder >> Result.toMaybe)
                                            |> Array.map Markdown.toHtml
                                            |> Layouts.Markdown.layout p "essays" publishedOn
                                    }
                                )

                [ "notes", baseName ] ->
                    model.notes.collection
                        |> Dict.get (Maybe.withDefault baseName (Url.percentDecode baseName))
                        |> Maybe.map
                                (\{ blocks, title, publishedOn } ->
                                    let
                                        p =
                                            Pages.note
                                                { baseName = baseName
                                                , title = title
                                                }
                                    in
                                    { page = p
                                    , view =
                                        blocks
                                            |> Array.filterMap (Json.Decode.decodeValue Markdown.jsonDecoder >> Result.toMaybe)
                                            |> Array.map Markdown.toHtml
                                            |> Layouts.Markdown.layout p "notes" publishedOn
                                    }
                                )

                _ ->
                    Pages.allPages
                        |> Array.findFirst (\{ route } -> route == pageRoute)
                        |> Maybe.map
                                (\p ->
                                    { page = p
                                    , view = Views.Pages.view viewOpts p
                                    }
                                )

        result =
            case maybe of
                Just a ->
                    { page = a.page
                    , view = a.view
                    }

                Nothing ->
                    { page = Pages.notFound
                    , view = Views.Pages.view viewOpts Pages.notFound
                    }
    in
    { title = result.page.title
    , body =
        result.view
            |> Array.map (Transmutable.Html.map ViewMsg)
            |> Array.map Transmutable.Html.VirtualDom.toVirtualDom
    }
